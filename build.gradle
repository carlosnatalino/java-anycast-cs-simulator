plugins {
    id 'net.nemerosa.versioning' version '2.6.1'
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

version = '0.1.1'

// ensures compatibility with Java 1.8
sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'simulator.cs.anycast.core.MultiThreadSimulator'

jar { // create a jar without the dependencies
    manifest {
        attributes(
            'Main-Class'     : mainClassName,
            'Built-By'       : System.properties['user.name'],
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Build-Revision' : versioning.info.commit,
            'Created-By'     : "Gradle ${gradle.gradleVersion}",
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

// create a single Jar with all dependencies
task fullJar(type: Jar) {
    manifest {
        attributes(
            'Main-Class'     : mainClassName,
            'Built-By'       : System.properties['user.name'],
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Build-Revision' : versioning.info.commit,
            'Created-By'     : "Gradle ${gradle.gradleVersion}",
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.0'
    // https://mvnrepository.com/artifact/com.lmax/disruptor required by log4j
    compile group: 'com.lmax', name: 'disruptor', version: '3.4.2'
    // https://mvnrepository.com/artifact/org.jgrapht/jgrapht
    compile group: 'org.jgrapht', name: 'jgrapht-core', version: '1.3.1'
    compile group: 'com.typesafe', name: 'config', version: '1.4.0'
    compile group: 'org.knowm.xchart', name:'xchart', version: '3.6.1'
    testCompile 'junit:junit:4.12'
}
