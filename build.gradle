plugins {
    id 'net.nemerosa.versioning' version '2.6.1'
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

mainClassName = 'simulator.cs.anycast.core.MultiThreadSimulator'

// create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes(
            'Main-Class'     : mainClassName,
            'Built-By'       : System.properties['user.name'],
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Build-Revision' : versioning.info.commit,
            'Created-By'     : "Gradle ${gradle.gradleVersion}",
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.0'
    // https://mvnrepository.com/artifact/org.jgrapht/jgrapht
    compile group: 'org.jgrapht', name: 'jgrapht', version: '0.9.2', ext: 'pom'
    testCompile 'junit:junit:4.12'
}
